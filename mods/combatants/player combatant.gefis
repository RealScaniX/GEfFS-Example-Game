This is player combatant by ScaniX

there is a Combatant called player combatant
	its attackProc is "playerAttack"
	its woundProc is "playerWound"
	its strength is 0
	its constitution is 0

function playerAttack
	do
		// find weapon
		now wpn is unarmed
		now ilist is [inventoryList]
		for each itm in ilist
			if itm is a weapon and itm.equipped
				wpn = itm
				break
		// say attack text
		invoke function wpn.attackProc
		// roll for hit
		if chance of 85 in (100 - player.glow + enemyLevel * 10)
			// crit?
			if chance of wpn.critChance
				say "Seeing an opening you take your chance and manage to hit the [+enemyName] at a vulnerable point."
				return this.strength * 2
			else
				// roll dice to get attack damage
				now dmgFactor is random number between 0.9 and 1.1
				now dmg is this.strength * dmgFactor
				invoke function wpn.hitProc
				return dmg
		else
			say "[one of]
				Miscalculating the movement of your opponent, you miss it by a few centimeters.[or]
				You underestimated the speed of the [+enemyName] and could not hit it.[or]
				The [+enemyName] deflected the blow.
				[at random]\n"
			return 0
		
function playerWound
	do
		now fearGain is dmg
		//debugSay "Player wound DMG: [+fearGain]"
		// dodge
		if !specialAttack && !poisonAttack and chance of player.glow in 100 + enemyLevel * 10
			say "[one of]Using your catlike reflexes, you dodge the attack and purr as you stay completely unharmed.
					[or]Noticing a pattern in the attack, you are able to avoid getting hit.[at random]\n"
			return false
		// reduce by glow
		fearGain = fearGain * (1.0 - player.glow / 120.0)
		//debugSay " glow reduction --> [+fearGain]"
		if fearGain <= 0
			say "The attack of the [+enemyName] is too weak to be able to scare you any longer."
			return false
		// reduce by block
		if !poisonAttack and this.defends
			fearGain = fearGain / 2
			say "Your defense [one of]is weakening[or]reduces the effect of[at random] the attack. "
		//debugSay " defense reduction --> [+fearGain]"
		if !poisonAttack
			if fearGain >= 40
				say "You are absolutely terrified by your opponent."
			else if fearGain >= 20
				say "The [+enemyName] scares you a lot."
			else if fearGain >= 7
				say "The [+enemyName] frightenes you."
			else 
				say "Your fear increases a little."
		if [scare fearGain]
			say "[one of]Your bravery is dwindling while the fear overcomes you.
					[or]The [+enemyName] is too strong for you, forcing you to surrender.
					[or]You lack the mental strength to push aside the fear for even a second longer.
					[at random]\n"
			say "\nYou are not sure how you got here, but you are lying on the floor in the entry hall again. Maybe you just ran [one of]like crazy[or]away in panic[at random] and 
					ended up collapsing in your safe place."
			return true
		return false
		