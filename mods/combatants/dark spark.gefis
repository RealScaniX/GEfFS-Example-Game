This is Dark Spark by ScaniX

there is a Combatant called dark spark
	its attackProc is "darkSparkAttack"
	its woundProc is "darkSparkWound"
	its introProc is "darkSparkIntro"
	its strength is 5
	its constitution is 20
	its level is 1
	its fightMusic is "Lomoto Perpetuo Loop.mp3"
	its lootTable is {"magic battery": -200, "antidote": 10, "music tape": 10, "battery": 3, "magic battery": -2}

function darkSparkIntro
	do
		say "You suddenly notice a point in front of you that seems to be darker than necessary."
		wait for 4 seconds
		say "You look around for something that might cast a shadow there, but cannot find anything. When it starts to move, you instinctively back off 
				and stare at the blackness."
		wait for 8 seconds
		say "As it detaches itself from the shadows, you can see that it looks like a big dark point with a cloud of smaller flecks around it. Each of them could be described
				as some sort of dark spark. Instead of emitting light, they seem to suck it in."
		wait for 12 seconds
		say "It obviously seems to be closing in on you, so you prepare to defend yourself."

function darkSparkAttack
	do
		if this.specialAttackPrepare > 0
			this.specialAttackPrepare = 0
			say "The dark spark's particles suddenly scatter, only to immediately gather around you again to enclose you in an "
			scare 0, "impenetrable darkness"
			say " for a few seconds.\n"
			return (random number between 0.9 and 1.1) * this.strength * 2
		else if this.specialAttackCooldown == 0 && chance of 40
			this.specialAttackPrepare = 1
			say "Instead of attacking you, the dark particle cloud seems to get ready for something."
			return 0
		else
			say "[+this.name] is using "
			scare 0, "flickering lights"
			say " to scare you."
			return (random number between 0.9 and 1.1) * this.strength

function darkSparkWound
	do
		this.hp = this.hp - dmg
		// drop green key
		if this.hp < 1 
			if !this.droppedItem
				say "The density of particles of the dark spark seems to have decreased below a value that is necessary for the mass attraction that keeps it together. Soon more and 
						more of the small antilights fly away from the cloud until they are all gone."
				if this.defeatedCount > 2 && chance of 70
					say "A purple key drops from within the darkness and falls to the floor."
					play "clong.ogg" on audio channel "sound" with volume 100
					add purple key to player.location.items
					this.droppedItem = true
				else if this.defeatedCount > 0
					say "You try to grab the purple item or get a better look at it, but you cannot reach it."
				else
					say "Just as your opponent slips away, you manage to spot a purple something in the dark cloud."
		else if !poisonAttack
			if dmg < 7
				say "The [+this.name]'s darkness loses density."
			else
				say "A few of [+this.name]'s particles got smashed."
			if this.hp < 7
				say "The dark spark flickers. Other than normal lights, this one becomes brighter when flickering."
		return this.hp < 1


