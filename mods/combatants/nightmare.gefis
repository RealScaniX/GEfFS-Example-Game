this is nightmare by scanix

there is a combatant called nightmare
	its constitution is 500
	its hp is 500
	its strength is 70
	its fightMusic is "Motherlode.mp3"		// start music manually later on
	its attackProc is "nightmareAttack"
	its woundProc is "nightmareWound"
	its introProc is null
	its appearProc is "nightmareAppear"
	its hugProc is "hugNightmareProc"
	its recoverProc is "recoverNightmare"

function recoverNightmare
	do
		now state is [nightmareState]
		if state is 4
			this.name = "Lucky"
			this.fightMusic = "Wounded.mp3"
		else
			this.name = "nightmare"
			this.fightMusic = "Motherlode.mp3"
		if state is 4
			this.hp = 1
		else if state is 3
			this.hp = this.constitution / 4 - 1
		else if state is 2
			this.hp = this.constitution / 2 - 1
		else if state is 1
			this.hp = this.constitution * 3 / 4 - 1
		else
			this.hp = this.constitution

function nightmareState
	do
		if nightmare.hp < 2
			return 4
		else if nightmare.hp < this.constitution / 4
			return 3
		else if nightmare.hp < this.constitution / 2
			return 2
		else if nightmare.hp < this.constitution * 3 / 4
			return 1
		else
			return 0

function nightmareAppear
	do
		now state is [nightmareState]
		if state is 4
			say "Your dog sits still in the same place as before and is waiting for you."
		else
			say "You are confronted with your worst nightmare."
		// reset strength
		this.strength = 60

function nightmareAttack
	do
		now state is [nightmareState]
		now monsterName is "[one of]creature[or]monster[or]enemy[or]opponent[or]attacker[or]nightmare[at random]"
		if state is 4
			say "The dog looks at you expectantly."
			return 0
		if this.specialAttackPrepare > 0
			this.specialAttackPrepare = 0
			if state < 2
				say "You watch in disgust as the tall [+monsterName] pulls out the metal rod sticking in its lungs."
				say "Armed with the thin metal, it hits you repeatedly and "
				scare 0, "hurts your arms and legs"
				say ". Your scar almost feels like it is opening again."
				say "After the attack is over, you cringe even more as you see it push the rod back in."
			else
				say "The [+monsterName] pounces you and you are thrown onto your back. "
				scare 0, "[one of]Seeing those sharp claws[or]Seeing those deadly fangs[or]Feeling the hot breath[at random]"
				say " so close to your face, lets you freeze in fear for a moment."
				say "Struggling with all your strength you are finally able to push off the beast with your feet."
			return (random number between 0.9 and 1.1) * this.strength * 2.5
		else if this.specialAttackCooldown == 0 && chance of 30
			this.specialAttackPrepare = 1
			if state < 2
				say "You see the [+monsterName] grab for something."
			else if state == 2
				say "You see your opponent go down onto all fours."
			else if state > 2
				say "The doglike creature is getting ready to jump."
			return 0
		else
			if state is 3
				if chance of 60
					say "You try to evade the attack and so it only grazes you. The fur of the creature runs across your skin in a "
					soothe 10, "familiar touch"
					say ".\n"
					return 0
				else
					say "The paw hits you in the chest, "
					scare 0, "hurting you a little"
					say " and pushing you back."
			else if state is 2
				if (chance of 40) and !currentFight.player.defends
					say "Although you jump back, the claws tear through your shirt and leave "
					scare 0, "deep scratches across your chest"
					say ".\n"
				else
					say "Unable to dodge the attack, you raise your arms in defense, only to receive "
					scare 0, "deep scratches across your forearms"
					say ".\n"
			else if state is 1
				say "Holding up your arm in defense, the [+monsterName] bites right into it."
				say "The "
				scare 0, "teeth penetrate your skin and flesh"
				say " deeper and deeper until repeated hits to its chest finally makes the monster draw back for the moment."
			else
				say "The [+monsterName] doesn't even move to attack you. A "
				scare 0, "horrible growl"
				say " is all it needs to make you cower in fear."
		return (random number between 0.9 and 1.1) * this.strength

function nightmareWound
	do
		now stateBefore is [nightmareState]
		if poisonAttack
			say "The poison of your weapon seem to have no effect."
			return false
		this.hp = this.hp - dmg
		if this.hp < 1
			this.hp = 1
		now state is [nightmareState]
		while stateBefore is not state
			stateBefore = stateBefore + 1
			if stateBefore is 4
				this.name = "Lucky"
				say "With the last attack you feel something going away. There is a sudden wave of darkness flow through you, then it is gone."
				say "As you focus on your opponent again, you see something that you've lost forever."
				say "The furry creature has changed back to its natural appearance. In front of you stands a large, golden retriever."
				if currentFight.events has "tapeBreaking"
					remove "tapeBreaking" from currentFight.events
				globals.fightMusic = "Wounded.mp3"
				playMusic null, 80
				playMusic globals.fightMusic, 80
			else if stateBefore is 3
				say "Your attack to the head has forced it to spin backwards. When it turns back towards you, you are shocked to see that the second eye is back in its socket
							and the bleeding has stopped."
				say "The body structure shifts a little and the creature seems to lose the balance as it goes down to all fours."
				say "The fur of the whole body suddenly seems less bloody and torn. Although this wasn't your intention, you begin to see a transformation to 
							something you long to see."
				//resume audio channel "music" with volume 20
			else if stateBefore is 2
				say "You hit the metal rod in its lungs and you see it completely sink into the body of the monster, only to leave it through its back and fly through the air.
							It never reaches the ground, though. As soon as it lost contact to the creature, it dissolves to nothing."
				//resume audio channel "music" with volume 40
			else if stateBefore is 1
				say "You are wondering if your attacks have any effect at all as you suddenly see some wounds heal up where you hit your opponent. 
							That is really not what you've expected."
				//resume audio channel "music" with volume 60
		this.strength = 60 - state * 13
		if this.strength < 1
			this.strength = 1
		return false

function hugNightmareProc
	do
		now state is [nightmareState]
		if state is 4
			currentFight.finished = true
			currentFight.won = true
			nightmare.killed = true
			currentFight.rewarded = true
			addOneTimeEvent "lettingGo"
		else if state > 0
			say "You slowly step closer, but a threatening growl lets you keep your distance again."
		else 
			say "You cannot bring yourself to step closer to the monster."

action hugNightmare
	understand
		hug dog
		hug lucky
		hug friend
		embrace dog
		embrace lucky
		embrace friend
		pet dog
		pet lucky
		pet friend
	do
		if currentFight.finished or !(currentFight.enemy is nightmare)
			say "You don't see that here."
			return
		now enm is nightmare
		invoke action "huggies"

function lettingGo
	do
		playMusic "Hero Down.mp3", 100
		scrollAway
		say "As you approach your best friend, he looks up at you and starts wagging his tail. You go down on your knees and embrace the furry body."
		wait for 8 seconds
		say "Your fingers run through the dense fur of your deceased buddy while tears swell in your eyes and run down your cheeks."
		wait for 8 seconds
		say "\"Please forgive me!\", you manage to say in between sobs."
		wait for 8 seconds
		say "As you think back at the horrible accident, those images are slowly pushed aside by other memories."
		wait for 8 seconds
		say "You see yourself and Lucky playing in the woods behind your old house. You see him next to your bed when you were ill, never leaving your side. And then you
				see your first memory of him. When your mother gave him to you on your seventh birthday."
		wait for 14 seconds
		say "That has been the luckiest day in your life. You've both been cubs and have played together for the whole day. There has been a bond between you and your dog
				from the very first moment on."
		wait for 12 seconds
		say "You feel his tongue lick across your face and then the scar on your arm."
		wait
		say "Suddenly your hands lose contact and when you open your eyes, you only see a cloud of lights swarming around you and finally into you."
		wait for 8 seconds
		say "You feel warmth, companionship and peace as you consume the light. Those new feelings soothe all your fears and regrets and although you are still sad 
				and miss your friend, you cannot help but smile and be thankful for the last moment you've shared with your friend."
		player.fear = 0
		player.glow = 100
		wait
		say "There is another glow coming from your hands and when you open your grip, you can see a key that seems to be made of light."
		wait for 8 seconds
		say "You finally stand up again, put the strange key into your pocket and wipe the tears from your face."
		addToInv key of light
		wait

