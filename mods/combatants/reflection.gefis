This is reflection by ScaniX

there is a Combatant called reflection
	its attackProc is "reflectionAttack"
	its woundProc is "reflectionWound"
	its introProc is "reflectionIntro"
	its strength is 20
	its constitution is 70
	its level is 3
	its lootTable is {"antidote": 10, "battery": 10, "green marble": -5, "magic battery": -2, "mirror shard": -2}

function reflectionIntro
	do
		say "From the corner of your eye you see a movement and slowly turn towards it.\nAt first you recognize yourself and relax. "
		wait for 5 seconds
		say "[backspace][backspace], but then you remember that there is no mirror [if player.location.name is \"bathroom\"]on that side[else]here[end]."
		wait for 3 seconds
		say "Slowly the movements of your reflection separate from your own as it changes into a threatening pose."

function reflectionAttack
	do
		if this.specialAttackPrepare > 0
			this.specialAttackPrepare = 0
			say "The living mirror in front of you suddenly smashes into dozens of pieces and when they all rotate to face you. Every single one of them shows a pair of eyes."
			say "You feel "
			scare 0, "pierced by countless eyes"
			say ".\n"
			return (random number between 0.9 and 1.1) * this.strength * 2.5
		else if this.specialAttackCooldown == 0 && chance of 30
			this.specialAttackPrepare = 1
			say "As you get ready for an incoming blow, you see that the reflection does something else instead."
			return 0
		else
			say "Your reflection is using a "
			scare 0, "guilty look"
			say " to make you feel bad."
			return (random number between 0.9 and 1.1) * this.strength

function reflectionWound
	do
		this.hp = this.hp - dmg
		// drop green key
		if this.hp < 1 
			if !this.droppedItem
				say "\n"
				say "A loud sound follows the cracks that spread across the whole reflection and signals the structural failure of your opponent. For a brief moment, 
							you can see a [one of]frightened[or]surprises[or]shocked[at random] face in the mirror, then it starts to collapse."
				if this.defeatedCount > 1 && chance of 70
					say "Some of the shards rejoin for a second to show the reflection of a [itemLink blue key]. Then it suddenly is like a projection into your side of the 
							mirror and materializes completely, before hitting the floor."
					play "clong.ogg" on audio channel "sound" with volume 100
					add blue key to player.location.items
					this.droppedItem = true
					currentFight.rewarded = true
				else if this.defeatedCount > 0
					say "You try to get a better view of the mysterious item, but all you get are a few glimpses of something that looks like a blue key."
				else
					say "As the attacker is smashed into thousand shards you see the reflection of something blue in some of them."
		else if !poisonAttack
			if dmg < 20
				say "The surface of the [+this.name] is becoming tarnished where it was hit."
			else
				say "A cracking sound indicates that your attack didn't go unnoticed."
			if this.hp < 20
				say "Cracks are spreading all over the reflection."
		return this.hp < 1
