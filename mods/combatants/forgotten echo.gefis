This is forgotten echo by ScaniX

there is a Combatant called forgotten echo
	its attackProc is "forgottenEchoAttack"
	its woundProc is "forgottenEchoWound"
	its introProc is "forgottenEchoIntro"
	its strength is 45
	its constitution is 300
	its level is 8
	its lootTable is {"music tape": 30, "battery": 3, "purple marble": -5, "magic battery": -2}

function forgottenEchoIntro
	do
		say "The sounds around you start to change somehow."
		wait for 5 seconds
		say "Even if you are not moving, you notice that some of the sounds that have been behind you a second ago, are now in front of you."
		wait for 8 seconds
		say "After concentrating on it for a moment, you are finally sure, that they are gathering."
		wait for 6 seconds
		say "Although you cannot really see anything, your ears are able to replace your eyes in this case and you are able to pinpoint the thing that
				is without any doubt a new enemy."
		wait

function forgottenEchoAttack
	do
		if this.specialAttackPrepare > 0
			this.specialAttackPrepare = 0
			play "snd1_x.ogg" on audio channel "sound"
			say "A shockwave of sounds "
			scare 0, "wipes you off your feet"
			say ". Lying on your stomach you feel very vulnerable and hastily stand up again."
			return (random number between 0.9 and 1.1) * this.strength * 2.5
		else if this.specialAttackCooldown == 0 && chance of 30
			this.specialAttackPrepare = 1
			say "As your ears prepare for the next attack, you notice that the little sounds around you start to gather in one point."
			return 0
		else
			say this.name + " is torturing you with the "
			scare 0, "sound of a [one of]terrible[or]painful[at random] memory"
			say ".\n"
			play "[one of]breakingGlass.ogg[or]155321__jace__dog-whining.ogg[or]110617__soundscalpel-com__horror-bone-crunch.ogg[or]190796__jiserte__xut1_ball_hit.ogg[or]
						99960__cgeffex__large-crash.ogg[or]screwShot.ogg[at random]" on audio channel "sound"
			return (random number between 0.9 and 1.1) * this.strength

function forgottenEchoWound
	do
		this.hp = this.hp - dmg
		// drop green key
		if this.hp < 1 
			say "\n"
			say "You seem to have distorted the airflow too much or produced too many sounds yourself, making the noises disband. You can hear them withdraw in different directions."
			if !this.droppedItem
				say "The sounds grow weaker and weaker, but suddenly they gather again for a last attack."
				say "A weak little shock wave is all they are capable of and it wasn't even directed at you."
				say "It blew up some dust from the floor and when it settled again, you see an [itemLink orange key] lying there. You wonder if it has been there the whole time."
				add orange key to player.location.items
				this.droppedItem = true
				currentFight.rewarded = true
		else if !poisonAttack
			if dmg < 50
				say "Disturbing the sound waves, you manage to make some of the sounds diminish."
			else
				say "Your attack causes a sound that overwhelms the sound cloud and mixes it up, so that it only produces an indistinguishable noise for a moment."
			if this.hp < 40
				say "The number of simultaneous sounds have decreased. The enemy is clearly weakened."
		return this.hp < 1

