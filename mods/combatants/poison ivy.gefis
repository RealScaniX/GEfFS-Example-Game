This is poison ivy by ScaniX

there is a Combatant called poison ivy
	its attackProc is "poisonIvyAttack"
	its woundProc is "poisonIvyWound"
	its introProc is "poisonIvyIntro"
	its appearProc is "poisonIvyAppear"
	its strength is 35
	its constitution is 120
	its level is 5
	its poisonous is true
	its lootTable is {"antidote": 40, "battery": 10, "orange marble": -5, "magic battery": -2}

function poisonIvyIntro
	do
		play "178615__montacue__the-rustle-of-a-bush.ogg" on audio channel "sound"
		say "A rustling sound startles you and as you look in its direction, you see some vines detach themselves from a small tree close to you."
		flush
		sleep 7
		say "Looking at the leaves you immediately recognize the poison ivy. You two have met before."
		flush
		sleep 5
		say "This one is a little different though. You watch the thin green wires with all those dangerous leaves form a humanlike body in front of you."
		flush
		sleep 7
		say "After it is finished you are standing in front of a two meters tall creature without a head, but with two legs, a bushy torso and four long arms that are
				whirling around in the air like whips."
		flush
		sleep 9
		say "Still you will not run home crying this time. You prepare to attack the plant to get revenge for what it has done to you."
		wait for 6 seconds

function poisonIvyAppear
	do
		play "178615__montacue__the-rustle-of-a-bush.ogg" on audio channel "sound"
		say "You would never have guessed that you would ever recognize a plant by its sound. Now you do and so you turn around to welcome the poison ivy."

function poisonIvyAttack
	do
		if this.specialAttackPrepare > 0
			this.specialAttackPrepare = 0
			say "Suddenly something is breaking out of the ground and grabs your ankles. Holding you in place with the vines that it has sent to you underground, you are unable
					to avoid the "
			scare 0, "repeated lashes"
			say " of its long arms."
			say "It takes you a few seconds to wiggle yourself out of the grasp of the evil plant."
			return (random number between 0.9 and 1.1) * this.strength * 2.5
		else if this.specialAttackCooldown == 0 && chance of 40
			this.specialAttackPrepare = 1
			say "The poison ivy does not directly attack you, but drives some of its vines into the ground. Probably to have a more stable posture."
			return 0
		else
			say "[+this.name] lashes out with its [one of]upper left[or]upper right[or]lower left[or]lower right[at random] arm whip to "
			scare 0, "spread its poison"
			say ".\n"
			return (random number between 0.9 and 1.1) * this.strength

function poisonIvyWound
	do
		if poisonAttack
			say "The poison has an unexpected, negative effect on the poison ivy. Well, negative for you. Instead of further wounding it, the poison effect seems to heal it instead."
			this.hp = this.hp + dmg
			if this.hp > this.constitution
				this.hp = this.constitution
			return
		this.hp = this.hp - dmg
		// drop green key
		if this.hp < 1 
			if !this.droppedItem
				say "Whatever has caused this plant to move on its own is no longer able to keep it up. It finally falls together to a heap of leaves and vines that gradually
						gets smaller as the leaves are shriveling to nothing. In the end there is nothing more than a small brown patch on the grass."
				if this.defeatedCount > 1 && chance of 70
					say "A green key lies in the patch. Having been nearly impossible to see before, its bright green color is making it stand out now."
					add green key to player.location.items
					this.droppedItem = true
				else if this.defeatedCount > 0
					say "You try to grab the green item or get a better look at it, but you cannot reach it."
				else
					say "Just as your opponent dissolves, you manage to spot a metallic green something in the vines for a second."
		else if !poisonAttack
			if dmg < 15
				say "Your attack breaks some smaller vines. This seems to be rather irritating for the plant, but does not seem to hurt much."
			else if dmg < 35
				say "Your [+currentFight.player.weapon.weaponName] [one of]rips off[or]cuts off[or]detaches[at random] some of the dangerous leaves."
			else
				say "[one of]breaking a vine[or]cutting off a twig[at random] of your opponent, you are glad to watch that part fall off and hit the ground, where it immediately
					starts to [one of]turn brown[or]weather[at random]."
			if this.hp < 30
				say "The leaves of the [+enemyName] start to shrivel and the movements begin to slow down."
		return this.hp < 1


