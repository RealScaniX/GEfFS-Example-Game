This is poison ivy by ScaniX

there is a Combatant called poison ivy
	its attackProc is "poisonIvyAttack"
	its woundProc is "poisonIvyWound"
	its introProc is "poisonIvyIntro"
	its strength is 30
	its constitution is 120
	its level is 5
	its poisonous is true
	its lootTable is {"antidote": 30}

there is an item called antidote
	its description is "It's a little flask with some greenish, thick fluid inside. On the outside there is a sticker with a single word: Antidote\n"
	its smell is "You take off the lid for a moment and take a whiff of it. You are totally sure that this is a prime example of a green smell. It strongly smells of all that
			nature stuff: Vegetables, herbs and other plants, mixed together with a breeze of fresh air."
	its taste is "This is some sort of medicine. You are not willingly tasting this until you are really sick."
	its sound is "If you squeeze it, the small plastic bottle does a little *click* sound."
	its combatUse is true
	its useProc is "useAntidote"

function useAntidote
	do
		if !currentFight.finished and (currentFight.player.poisonDOT > 0 or player.fear > 15)
			say "With a sigh you open the antidote and pour the fluid into your mouth. You take big gulps to avoid tasting it too much."
			wait for 5 seconds
			say "You feel the sickness shy away from the power of natural life force. Just one or two seconds later the "
			soothe 15, "pleasant tingle"
			say " has spread out through your whole body and the bad feeling is gone."
			wait for 5 seconds
			say "When you want to throw away the empty bottle, you notice that there isn't anything in your hand anymore."
		else
			say this.taste

understand 
	"drink antidote" as "use antidote"

function poisonIvyIntro
	do
		say "You suddenly notice a point in front of you that seems to be even darker than its surroundings.
				When it starts to move, you brace yourself for an attack."

function poisonIvyAttack
	do
		if this.specialAttackPrepare > 0
			this.specialAttackPrepare = 0
			say "The dark spark's particles scatter only to gather around you again and enclose you in an "
			scare 0, "impenetrable darkness"
			say " for a second.\n"
			return (random number between 0.9 and 1.1) * this.strength * 2
		else if this.specialAttackCooldown == 0 && chance of 40
			this.specialAttackPrepare = 1
			say "Instead of attacking you, the dark particle cloud seems to get ready for something."
			return 0
		else
			say this.name + " is using "
			scare 0, "flickering lights"
			say " to scare you."
			return (random number between 0.9 and 1.1) * this.strength

function poisonIvyWound
	do
		if dmg < 7
			say "The [+this.name]'s darkness loses density."
		else
			say "A few of [+this.name]'s particles got smashed."
		this.hp = this.hp - dmg
		if this.hp < 7
			say "The dark spark flickers. Other than normal lights, this one becomes brighter when flickering."
		// drop green key
		if this.hp < 1 and !this.droppedItem
			say "The density of particles of the dark spark seems to have decreased below a value that is necessary for the mass attraction that keeps it together. Soon more and 
					more of the small antilights fly away from the cloud until they are all gone."
			if this.encounterCounter > 2 && chance of 70
				say "A green key drops from within the darkness and falls to the floor."
				play "clong.ogg" on audio channel "sound" with volume 100
				add green key to player.location.items
				this.droppedItem = true
			else if this.encounterCounter > 1
				say "You try to grab the green item or get a better look at it, but you cannot reach it."
			else
				say "Just as your opponent slips away, you manage to spot a metallic green something in the vines."
		return this.hp < 1


