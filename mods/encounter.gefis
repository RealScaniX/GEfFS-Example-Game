This is Encounter by ScaniX

An Encounter is an Entity
	attributes
		Combatant enemy
		number probability

A Place
	attributes
		list encounters
		bool pacified is false

before function mainPrompt
	if temps.encounterCooldown > 0
		temps.encounterCooldown = temps.encounterCooldown - 1
	//else if user.action is null or user.action is "lookat" or user.action is "lookaround"
	else if user.action is null // need more fights
		// no encounter when looking around
	else if !player.location.pacified and !nightmare.killed
		now max is 0
		now nix is 0
		for each enc in player.location.encounters
			max = max + 100
			nix = nix + (100 - enc.probability)
		now z is random number between 0 and max
		if z >= nix
			z = z - nix
			for each enc in player.location.encounters
				if z < enc.probability and enc.enemy.wantsFight
					// start fight
					startFight enc.enemy
					return
				z = z - enc.probability

before function startFight
	temps.encounterCooldown = 10

function addEncounter
	understand
		addEncounter [reg], [enmy], [prob]
	do
		if devmode and enmy is not a combatant
			say "Error: The [+enmy.name] is not a combatant."
			return
		if enmy is not nightmare
			say "You cannot shake off the feeling that your [one of]presence has[or]actions have[at random] just awakened something."
		now enc is a new encounter called (enmy.name + " encounter")
		enc.probability = prob
		enc.enemy = enmy
		now placelist is place.*
		// add garden
		if globals.gardenTiles is not null
			for each row in globals.gardenTiles
				for each t in row
					if t is not garden // already in list
						add t to placelist
		for each p in placelist
			if reg is null or p.regions has reg
				add enc to p.encounters
		if temps.encounterCooldown < 5
			temps.encounterCooldown = 5

function removeEncounter
	understand
		removeEncounter [enmy]
	do
		if devmode and enmy is not a combatant
			say "Error: The [+enmy.name] is not a combatant."
			return
		if enmy is not nightmare and enmy is not death
			say "Something is different this time as you raise your fist in victory. You somehow sense that this creature has gone back to sleep for good now."
		now placelist is place.*
		// add garden
		if globals.gardenTiles is not null
			for each row in globals.gardenTiles
				for each t in row
					if t is not garden // already in list
						add t to placelist
		now encname is enmy.name + " encounter"
		for each p in placelist
			remove encname from p.encounters

when play starts or resumes
	temps.encounterCooldown = 3

/***********************************************************
 * No encounter during these actions
 */
before action about
	temps.encounterCooldown = temps.encounterCooldown + 1

before action help
	temps.encounterCooldown = temps.encounterCooldown + 1

before action inventorying
	temps.encounterCooldown = temps.encounterCooldown + 1
