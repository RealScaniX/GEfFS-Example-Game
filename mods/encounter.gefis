This is Encounter by ScaniX

An Encounter is an Entity
	attributes
		Combatant enemy
		number probability

A Place
	attributes
		list encounters
		bool pacified is false

before function mainPrompt
	if player.location is cellar
		temps.encounterCooldown = 0
	if user.action is "lookat" or user.action is "lookaround"
		// no encounter when looking around
	else if temps.encounterCooldown > 0
		temps.encounterCooldown = temps.encounterCooldown - 1
	else if !player.location.pacified
		now max is 0
		now nix is 0
		for each enc in player.location.encounters
			max = max + 100
			nix = nix + (100 - enc.probability)
		now z is random number between 0 and max
		if z >= nix
			z = z - nix
			for each enc in player.location.encounters
				if z < enc.probability
					// start fight
					temps.encounterCooldown = 10
					startFight enc.enemy, true
					return
				z = z - enc.probability

function addEncounter
	understand
		addEncounter [reg], [enmy], [prob]
	do
		if enmy is not a combatant
			say "Error: The [+enmy.name] is not a combatant."
			return
		if enmy is not nightmare
			say "You cannot shake off the feeling that your presence has has just awakened something."
		now enc is a new encounter called (enmy.name + " encounter")
		enc.probability = prob
		enc.enemy = enmy
		for each p in place.*
			if reg is null or p.regions has reg
				add enc to p.encounters
		if temps.encounterCooldown < 5
			temps.encounterCooldown = 5

when play starts or resumes
	temps.encounterCooldown = 3
