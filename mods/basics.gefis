This is gefis one basics by ScaniX

There is a ValueSponge called Globals
	its chapter is 0

There is a ValueSponge called temps
	it is static

when play begins
	stop audio channel "music"
	stop audio channel "sound"
	stop audio channel "ambience"

when play resumes
	stop audio channel "sound"

Entity Thing is an Entity
   attributes
		bool discovered is true
		text description
		text smell is "This smells like nothing really."
		text taste is "You'd rather not taste that. For now."
		list actions are {"look at"}

entity Item is a Thing
	its actions are {"look at", "take"}
   attributes
		bool carryable is true
      number weight
		text useFunction

entity Sound is a Thing
	its actions are {"listen to"}

entity Equippable is a Thing
	attributes
		bool equipped
		text equipmethod	// name of function to put this on

entity Weapon is a Equippable
	its equipmethod is "equipweapon"

entity Armor is a Equippable
	its equipmethod is "equiparmor"
	attributes
		list bodyparts		// "head", "top", "legs", "hands", "waist": you cannot wear two armors at the same time that cover the same region

entity playercharacter is a thing
	attributes
		place location
		number fear is 30
		number glow is 0  // the inner light value of the player

there is a playercharacter called Player
	its location is entry hall

action lookat
	understand
		look [smt]
		look at [smt]
		look at the [smt]
	do
		if player.illumination < 5
			say "In the total darkness you see totally nothing."
			return
		// can you see that?
		now smt is [resolveitem smt]
		if smt is a sound
			say "Looking at a sound makes your head feel dizzy..."
		else if smt is a thing
			say smt.description
			// add contents
			if smt is a container and smt.listContentsOnLook
				listContents smt
		else
			say "You don't see that."

action smell
	understand
		smell [smt]
	do
		// can you see that?
		now smt is [resolveitem smt]
		if smt is a sound
			say "The sound smells kind of green. With a little bit of monday morning mixed into it."
		else if smt is a thing
			say smt.smell
		else
			say "You don't see that."

action taste
	understand
		taste [smt]
	do
		// can you see that?
		now smt is [resolveitem smt]
		if smt is a sound
			say "The sound tastes like no other sound that you've tasted before. Well, except the one of those church bells."
		else if smt is a thing
			say smt.taste
		else
			say "You don't see that."

function visibleitemlist
	do
		now itemlist is a new list
		// items of place
		for each itm in player.location.items
			if itm.discovered
				add itm to itemlist
				addContainerItems itm, false
		// passages of place
		//for each itm in player.location.exits
		//	if itm is a passage
		//		add itm to itemlist
		return itemlist

function invItemLink
	understand
		invItemLink [itm]
	do
		return "<link [for each act in itm.actions][if not (act is \"take\")][append act]|[end][end]>" + itm.name + "</link>"

function resolveitem
	understand
		resolveitem [itm]
	do
		now vlist is [visibleitemlist]
		// add inventory stuff
		for each x in player.inventory
			add x to vlist
			now itemlist is vlist						// workaround until byref of arrays work in functions
			addContainerItems x, true //, vlist    // we create a local that is resolvable from inside of the function addContainerItems
			now vlist is itemlist						// then put the results back into our local variable
		// add location
		add player.location to vlist
		if itm is a text
			now itm is a new Phrase called itm
		if itm is a Phrase
			if vlist has itm
				now itm is vlist.(key of itm in vlist)
			else
				now itm is null
		else if not (vlist has itm)
			now itm is null
		return itm

action open
	understand
		open [d]
	do
		now d is [resolveitem d]
		if d is null
			say "You don't see that here."
		else if d is a Door
			setDoor d, true
		else if d is a container
			setContainer d, true
		else
			say "You don't know how to open this."

action close
	understand
		close [d]
	do
		now d is [resolveitem d]
		if d is null
			say "You don't see that here."
		else if d is a Door
			setDoor d, false
		else if d is a container
			setContainer d, false
		else
			say "You don't know how to close this."

action unlockwithkey
	understand
		unlock [d] with [k]
		unlock the [d] with the [k]
		unlock [d] with a [k]
		unlock the [d] with a [k]
	do
		now d is [resolveitem d]
		now k is [resolveitem k]
		if d is null
			say "You don't see that here."
			return
		if k is null
			say "You don't see that key here."
			return
		if not (k is a key)
			say "You cannot open anything using [a|an][+k.name]."
			return
		if not (player.inventory has k)
			say "[one of]Wishful thinking will not let that key materialzie in your hand. Or will it?[or]Sticking your hand into your pocket you regretfully realize that you don't have this key.[at random]"
			return
		if d is a door or d is a container
			if not d.locked
				say "You (or someone else) already successfully unlocked the [+d.name]."
			else if d.key is k
				now d.locked is false
				say "You successfully unlock the [+d.name] using the [+k.name]."
			else
				say "[one of]You try every possible angle, but that key will not open the [+d.name].[or]Sheer force will not make that key fit either. The [+d.name] stays locked.[at random]"
		else
			say "You don't know how to unlock this with the [+k.name] (or anything else for that matter)."

action use
	understand
		use [t]
	do
		now t is [resolveitem t]
		if t is null
			say "You don't see that here."
		else if t is a Door
			setDoor t, (not t.open)
		else if t is a container
			setContainer t, (not t.open)
		else if t has "useFunction"
			invoke function t.useFunction on t
		else
			say "You don't know how to use this."

action help
	do
		say "<b>Help</b>\n"
		say "To accomplish things in this game, you can use simple sentences like in the old adventure games.\nDon't try some complex stuff though or the game wouldn't understand you
				and answer with something lame and dissappointing.\n"
		say "\n<div style=\"background-color:rgba(120, 120, 120, 0.2);\">"
		say "<b>\></b> <i>Grab that bloody door unlocking device from behind that fricking cell bars</i>\n"
		say "Sorry, but I couldn't understand you.\n"
		say "</div>"
		say "\nBut if you keep the sentence short and precise, your actions will have a higher chance of success."
		say "\n<div style=\"background-color:rgba(120, 120, 120, 0.2);\">"
		say "<b>\></b> <i>take red key</i>\n"
		say "You cannot get the stupid key, because the accursed cell bars are in the way!\n"
		say "</div>"
		say "\nYeah, well... I bet you get the point anyway."
		say "\n<hr/>\n"
		say "Here is a list of actions that can be performed in the game:"
		say "look at <i>something</i>\n"
		say "unlock <i>something</i> with <i>some key</i>\n"
		say "smell <i>something</i>\n"
		say "taste <i>something</i>\n"
		say "close <i>something</i>\n"
		say "open <i>something</i>\n"
		say "take <i>something</i>\n"
		say "drop <i>something</i>\n"
		say "put <i>something</i> into <i>somewhere</i>\n"
		say "go <i>somewhere</i>\n"

understand
	"look" as "look around"

function saychapter
	understand
		saychapter [chap]
	do
		sleep 0.7
		play "elevatoreyeOpenClose.mp3" on audio channel "sound" with volume 10
		flush
		sleep 0.7
		now chapternames is {"A simple task", "The circle of keys"}
		say "\n\n"
		say "<div style=\"text-align:center;\">"
		say "<span style=\"font-size:38px;letter-spacing:2px;\">[if chap > 0]Chapter [+chap][else]Prologue[end]</span>\n"
		say "<span style=\"font-size:24px;letter-spacing:4px;font-style:italic;\">[+chapternames.(chap)]</span>\n"
		say "<image \"chapter.png\">\n"
		say "</div>"
		say "\n\n"
		flush
		sleep 2

function playmusic
	understand
		playmusic [track]
	do
		//say "play music: [+track], current music: [+globals.musictrack]\n"
		if globals.musictrack is track
			return
		if track is null
			stop audio channel "music"
		else
			loop track on audio channel "music" with volume 45
		now globals.musictrack is track

function playambience
	understand
		playambience [track]
	do
		if globals.ambiencetrack is track
			return
		if track is null
			stop audio channel "ambience"
		else
			loop track on audio channel "ambience" with volume 90
		now globals.ambiencetrack is track

