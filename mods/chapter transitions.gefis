this is chapter transitions by ScaniX

function chapterAdvance
	do
		cleanupBubbles
		globals.chapter = globals.chapter + 1
		if 0 is globals.chapter
			saychapter 0
			playmusic "Quinns Song-First Night.mp3", 100
			playambience "27077__daveincamas__clockticks.ogg", 10
			prepareBubbles
			introtalk
			cleanupBubbles
			say "You barely listen to the voices behind you. Your heart is thumping in your chest as you are standing alone in this big abandoned house. It is close to midnight and
					you sneaked out of your bedroom to be here."
			say "All you can think about is that this is the worst thing they could have asked you to do. Your fear of the dark has been a problem ever after that accursed accident."
			wait for 20 seconds
			say "Your mind travels back in time and you see that black nothingness in front of you again while in reality you are still standing in a big moonlit rectangle on the
					floor with the shadow of a boy inside of it."
			playambience "27077__daveincamas__clockticks.ogg", 20
			wait
			say "As if adjusting to your imagination, the rectangle continuously becomes narrower until it is a thin line and then vanishes completely."
			playambience "27077__daveincamas__clockticks.ogg", 30
			wait for 15 seconds
			play "entrydoor.mp3" on audio channel "sound" with volume 100
			playambience "27077__daveincamas__clockticks.ogg", 40
			flush
			say "The heavy wooden door closes behind you with an ear shattering sound that echoes in this huge hall for what feels like minutes to you. Motionless you keep standing in
					the dark, listening to the sounds of anything that could have been awakened by this noise."
			say "But all you can hear is your own breathing and some ticking sound. At least for now."
			playambience "27077__daveincamas__clockticks.ogg", 50
			wait
			goToPlace entry hall
			user.actions = 0
			switch to main mode
		else if 1 is globals.chapter
			// transform places
			cellar.illumination = 30
			cellar door.description = @[cellardoordesc]
			cellar door.open = false
			cellar door.locked = true
			cellar door.key = null
			cellar door.destination = alternative entry hall
			cellar.music = "Quinns Song-A New Man.mp3"
			//cellar.events = {} // already done by setting the repeat to false
			//addEncounter "cellar", nightmare, 100
			player.location = alternative entry hall // to not directly trigger finalfight (in fact this does not happen anyway)
			add finalFightEvent to cellar.events
			kitchen door.destination = alternative entry hall
			kitchen.ambience = "27077__daveincamas__clockticks_noecho.ogg"
			ticking sound.soundDesc = "The loud ticking of the grandfather clock tells you that you are still in the entry hall. Well, ... at least <b>an</b> entry hall."
			playmusic alternative entry hall.music, alternative entry hall.musicVolume
			playambience null, 100
			saychapter globals.chapter
			scare 10
		else if 2 is globals.chapter
			saychapter globals.chapter
		else if 3 is globals.chapter
			saychapter globals.chapter
		else if 4 is globals.chapter
			playMusic null, 100
			playAmbience null, 100
			stop audio channel "sound"
			saychapter globals.chapter

function scrollAway
	do
		story log off
		for i from 1 to 30
			say "\n\n"
			flush
			sleep 0.2
		story log on
		cls

function fadeOut
	do
		show "" in overlay "nw" with style "background:white;border:none;width:100%;height:100%;"
		flush
		sleep 1

function fadeIn
	do
		hide overlay "nw" with style "default"
		flush

function saychapter
	understand
		saychapter [chap]
	do
		fadeOut
		cls
		sleep 0.7
		play "elevatoreyeOpenClose.mp3" on audio channel "sound" with volume 10
		flush
		sleep 0.7
		now chapternames is {"A simple task", "The circle of keys", "Somewhere green", "Letting go", "Welcome to the club"}
		say "\n\n"
		say "<div style=\"text-align:center;\">"
		say "<span style=\"font-size:38px;letter-spacing:2px;\">[if chap == 4]Epilogue[else if chap > 0]Chapter [+chap][else]Prologue[end]</span>\n"
		say "<span style=\"font-size:24px;letter-spacing:4px;font-style:italic;\">[+chapternames.(chap)]</span>\n"
		say "<image chapter.png>\n"
		say "</div>"
		say "\n\n"
		flush
		fadeIn
		sleep 2
