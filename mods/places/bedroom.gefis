this is bedroom by scanix

there is a place called bedroom
	its exits are {"south": westwingNorthDoor}
	its items are {bed, bedroomWardrobe, left nightstand, right nightstand}
	its music is "Quinns Song-First Night.mp3"
	its intro is @[bedroomIntro]
	its illumination is 10
	its regions are {"anywhere", "house", "second floor"}
	its description is @[bedroomDesc]

there is a container called bedroomWardrobe
	its name is "wardrobe"
	its description is @[bedroomWardrobeDesc]
	its discovered is false
	its openSound is "wardrobe.ogg"
	its closeSound is "wardrobe.ogg"

function bedroomDesc
	do
		say "There are windows to the west and north. At least the window on the western wall lets in some of the moonlight, while the window to the north 
				shows the garden."
		say "A large bed with all the necessary bedclothes and an additional layer of dust, a large wardrobe and two nightstands are all that keeps this 
				room from being empty."
		say "The pictures on the wall are as interesting as those on the wall of the waiting room of your former dentist. Just to be clear: 
				That means that they are boring as hell."

function bedroomWardrobeDesc
	do
		say "The large wardrobe almost touches the ceiling. It is made of some old, sturdy and dark wood."
		if this.open
			say "Seeing that there is nothing inside, you are relieved that the former owners at least took their clothes with them and are not running around 
					naked out there somewhere."
		else
			say "There is a small keyhole with a key in it, so opening it shouldn't be a problem."

there is a container called left nightstand
	its description is @"It's a small wooden table with some drawers and a lamp on top of it.[if this.open] You can see a magazine about cars, some advertisements for a finance
			software and a small black book with business cards. Can it get any more boring than that?[end]"
	its listContentsOnLook is false
	its showDescritionOnOpen is true
	its discovered is false
	its openSound is "drawer_open.ogg"
	its closeSound is "drawer_close.ogg"

there is a container called right nightstand
	its description is @"It's a small wooden table with some drawers and a lamp on top of it.[if this.open] In the drawers there is some uninteresting adult stuff like a few books,
			glasses, a pen and a crosswords puzzle magazine.[if !globals.rubberStickExamined] Oh, but what's that?[end][end]"
	its items are {vibrator}
	its discovered is false
	its showDescritionOnOpen is true
	its openSound is "drawer_open.ogg"
	its closeSound is "drawer_close.ogg"

there is a thing called vibrator
	it is hidden  // oh, yes
	its name is "rubber stick"
	its description is @[rubberStickDesc]
	its smell is "Smells like rubber."
	its taste is "Mmh... no."
	its useProc is "useVibrator"
	its sound is @"[if globals.rubberStickExamined]It does not make a sound when it's turned off[else]It doesn't make a sound[end]."
	
function useVibrator
	do
		if !globals.rubberStickExamined
			invoke function "rubberStickDesc"
		else
			say "You have no need for a massage right now."

function bedroomIntro
	do
		westwingNorthDoor.name = "bedroom door"
		say "The big double bed in the middle and the boringness of the pictures on the wall tells you that you have now entered the parents bedroom."

function rubberStickDesc
	do
		if !globals.rubberStickExamined
			addOneTimeEvent "rubberStickExamine"
		else
			return "Now that you have the battery, you cannot think of anything else to do with that."

function rubberStickExamine
	do
		globals.rubberStickExamined = true
		say "It looks like a gray, short stick. You take the weird thing into your hand and turn it around. It feels a bit rubbery and isn't very heavy."
		wait for 5 seconds
		say "Wondering what that might be good for, you finally notice a switch on the bottom."
		wait
		loop "181493__toiletrolltube__vibrator-26-1-11.ogg" on audio channel "sound" with volume 80
		flush
		sleep 1
		say "You almost drop it when it starts to vibrate in your hand. For some reason you start to look around for anyone who might have noticed that sound."
		flush
		sleep 5
		say "You wait for a few seconds, but that seems to be all that thing is capable of. From time to time the frequency seems to change, but that is all.
				You come to the conclusion that this is used to massage sore muscles or something like that."
		wait
		stop audio channel "sound"
		say "You turn it off again and are just about to put it back, when you have an idea."
		wait
		say "You open the lid on the bottom and take out the battery. At least that part might come in handy."
		addToInv battery
