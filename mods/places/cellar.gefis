This is Cellar by ScaniX

there is a place called Cellar
	its music is "edjaGhosty.ogg"
	its exits are {"up": cellar door}
	its items are {cellar door}
   its description is "This is without any doubt the cellar. There is - if that is possible - even more dust here than above. You can see a broken light bulb in the middle of
							the room, hanging from an old cable.\nThe dominating feature however is a large amount of boxes which is almost covering the whole floor. They are also stapled
							up to the ceiling or at least to a height over your head, so that you can barely see anything of the walls."
	its intro is @[cellarintro]
	its regions are {"cellar"} // no "house" region as we want exactly one opponent here

function cellarintro
	do
		say "The cellar. If the boys did their job right, the ball would be somewhere around here for sure."
		say "You really cannot help, but even a boy your age without a history like yours would get "
		scare 10, "frightened"
		say " down here."

there is a doorPassage called cellar door
	its origin is cellar
	its description is @[cellarDoorDesc]
	its destination is entry hall
	its open is false
	its locked is true
   its key is rusty key

function cellarDoorDesc
	do
		if globals.chapter < 1
			if cellar.known
				append "That is the cellar door. You've already verified that. It is currently [if this.open]open[else]closed[end]."
			else
				append "Being placed on the side of the stairway, this should lead down into the cellar. It looks a bit older and the door handle and lock are both a bit rusty."
		else
			append "The cellar door has gone through some changes as well. The old lock has gone and has been replaced by five metal bars in different colors."
			now barCount is 5 - globals.openedCellarBars
			append "\n[one of]At the moment[or]Currently[or]Now[at random] [+barCount] of them [if barCount == 1]is[else]are[end] closed."

there is a key called rusty key
	its discovered is false
   its description is "You see a pretty rusty key."

when play starts or resumes
	now ev is a new event called "cellarEvent"
	ev.delay = 0
	ev.probability = 100
	ev.always = true
	ev.repeat = true
	ev.eventproc = "cellarEvent"
	add ev to cellar.events
	globals.cellarEvent = 0
	// stuff
	now crates is a new thing called "crates"
	crates.description = "They are closed and probably full of crap the old owner did not want anymore. The thick layer of dust let's you assume that the ball isn't hidden in there."
	crates.discovered = false
	add crates to cellar.items

function cellarEvent
	do
		globals.cellarEvent = globals.cellarEvent + 1
		if globals.cellarEvent is 1
			wait for 12 seconds
			say "Where the hell do those "
			scare 20, "freaky sounds"
			say " come from? "
			flush
			sleep 3
			say "Some old chimney? "
			flush
			sleep 1
			say "Water pipes? "
			flush
			sleep 1
			say "."
			flush
			sleep 0.3
			say "."
			flush
			sleep 0.3
			say ". "
			flush
			sleep 0.3
			say "Monsters?"
			this.delay = 1
			if player.light is not null
				now player.light.usedSteps is 2
		else if globals.cellarEvent is 2
			wait for 5 seconds
			play "doorClose.ogg" on audio channel "sound" with volume 100
			now cellar door.open is false
			now cellar door.locked is true
			removeFromInv rusty key
			removeFromInv box of matches
			flush
			sleep 1
			say "What!? No!!! "
			scare 35, "Someone closed the cellar door.\n"
			wait for 3 seconds
			say "\"This isn't funny anymore.\", you shout out into the dark, \"Who is up there?\"\n"
			wait for 6 seconds
			say "When there is no answer, you run up the stairs to open the door. "
			wait for 3 seconds
			play "doorLocked.mp3" on audio channel "sound"
			flush
			sleep 0.8
			say "No. "
			play "doorLocked.mp3" on audio channel "sound"
			flush
			sleep 0.7
			say "No. "
			play "doorLocked.mp3" on audio channel "sound"
			flush
			sleep 0.6
			say "No! "
			wait for 2 seconds
			say "Why is it locked now?"
			wait for 2 seconds
			say "\"Hello?\", you shout again, \"Come on, let me out of here!\"\n"
			this.delay = 0
		else if globals.cellarEvent is 3
			say "You fumble around in your pockets, but the key is gone. It is probably still sticking in the lock from the outside."
			this.delay = 2
			if player.light is not null
				now player.light.usedSteps is 4
		else
			addToInv box of matches
			play "cat.ogg" on audio channel "sound"
			flush
			sleep 0.7
			say "\"Whoah!\", you jumped at the "
			scare 4, "sudden sound"
			say ". The fact that it was only a cat - wherever it suddenly appeared from - doesn't help you anymore as you are already too frightened."
			wait for 15 seconds
			say "The positive things are: It is already pitch black here, so there is no difference when the world goes black in front of your eyes. Another nice thing is
					that the crate you just collapsed upon seems to be stuffed with soft things."
			wait
			chapterAdvance
