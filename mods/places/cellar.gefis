This is Cellar by ScaniX

there is a place called Cellar
	its music is "edjaGhosty.ogg"
	its exits are {"up": cellar door}
	its items are {cellar door}
   its description is "This is without any doubt the cellar. There is - if that is possible - even more dust here than above. You can see a broken light bulb in the middle of
							the room, hanging from an old cable.\nThe dominating feature however is a large amount of boxes almost covering the whole floor. They are also stapled
							up to the ceiling or at least to a height over your head, so that you can barely see anything of the walls."
	its intro is @[cellarintro]
	its regions are {"cellar"} // no "house" region as we want exactly one opponent here

function cellarintro
	do
		say "The cellar. If the boys did their job right, the ball would be somewhere around here for sure."
		say "You really cannot help, but even a boy your age without a history like yours would get "
		scare 10, "frightened"
		say " down here."

before action close
	if d is cellar door and d.open and player.location is cellar
		say "You don't want to close the cellar door as long you are in the cellar."
		return

there is a doorPassage called cellar door
	its origin is cellar
	its description is @[cellarDoorDesc]
	its destination is normalEntryHall
	its open is false
	its locked is true
   its key is rusty key

function cellarDoorDesc
	do
		if globals.chapter < 1
			if cellar.known
				append "That is the cellar door. You've already verified that. It is currently [if this.open]open[else]closed[end]."
			else
				append "Being placed on the side of the stairway, this should lead down into the cellar. It looks a bit older and the door handle and lock are both a bit rusty."
		else
			append "The cellar door has gone through some changes as well. The old lock has gone and has been replaced by five metal bars in different colors."
			now barCount is 5 - globals.entranceLocksOpened
			append "\n[one of]At the moment[or]Currently[or]Now[at random] [if barCount == 5]all[else][+barCount][end] of them [if barCount == 1]is[else]are[end] closed."

there is a key called rusty key
	its discovered is false
   its description is "You see a pretty rusty key."

when play starts or resumes
	now ev is a new event called "cellarEvent"
	ev.delay = 0
	ev.probability = 100
	ev.always = true
	ev.repeat = true
	ev.eventproc = "cellarEvent"
	add ev to cellar.events
	globals.cellarEvent = 0
	// stuff
	now crates is a new thing called "crates"
	crates.description = "They are closed and probably full of crap the old owner did not want anymore. The thick layer of dust let's you assume that the ball isn't hidden in there."
	crates.discovered = false
	add crates to cellar.items

function cellarEvent
	do
		globals.cellarEvent = globals.cellarEvent + 1
		if globals.cellarEvent is 1
			wait for 12 seconds
			say "Where the hell do those "
			scare 20, "freaky sounds"
			say " come from? "
			flush
			sleep 3
			say "Some old chimney? "
			flush
			sleep 1
			say "Water pipes? "
			flush
			sleep 1
			say "."
			flush
			sleep 0.3
			say "."
			flush
			sleep 0.3
			say ". "
			flush
			sleep 0.3
			say "Monsters?"
			this.delay = 1
			if player.light is not null
				now player.light.usedSteps is now player.light.steps - 2
		else if globals.cellarEvent is 2
			wait
			play "doorM_close_new.ogg" on audio channel "sound" with volume 100
			now cellar door.open is false
			now cellar door.locked is true
			removeFromInv rusty key
			removeFromInv box of matches
			flush
			sleep 1
			say "What!? No!!! "
			scare 35, "Someone closed the cellar door.\n"
			wait for 3 seconds
			say "\"This isn't funny anymore.\", you shout out into the dark, \"Who is up there?\"\n"
			wait for 6 seconds
			say "When there is no answer, you run up the stairs to open the door. "
			wait for 3 seconds
			play "doorLocked.mp3" on audio channel "sound"
			flush
			sleep 0.8
			say "No. "
			play "doorLocked.mp3" on audio channel "sound"
			flush
			sleep 0.7
			say "No. "
			play "doorLocked.mp3" on audio channel "sound"
			flush
			sleep 0.6
			say "No! "
			wait for 2 seconds
			say "Why is it locked now?"
			wait for 2 seconds
			say "\"Hello?\", you shout again, \"Come on, let me out of here!\"\n"
			this.delay = 0
		else if globals.cellarEvent is 3
			say "You fumble around in your pockets, but the key is gone. It is probably still sticking in the lock from the outside."
			this.delay = 2
			if player.light is not null
				now player.light.usedSteps is 4
		else
			this.repeat = false // no more triggers
			addToInv box of matches
			play "cat.ogg" on audio channel "sound"
			flush
			sleep 0.7
			say "\"Whoah!\", you jumped at the "
			scare 1, "sudden sound"
			say ". The fact that it was only a cat - wherever it suddenly appeared from - doesn't help you anymore as you are already too frightened."
			player.fear = 99
			wait for 15 seconds
			say "The positive things are: It is already pitch black here, so there is no difference when the world goes black in front of your eyes. Another nice thing is
					that the crate you just collapsed upon seems to be stuffed with soft things."
			wait
			chapterAdvance

there is an event called finalFightEvent
	its probability is 100
	its delay is 0
	its repeat is true
	its always is true
	its eventProc is "finalFightEventProc"

function finalFightEventProc
	do
		if nightmare.encounterCounter == 0
			say "As you enter the cellar for the second time, everything is different. Nothing looks the same."
			flush
			sleep 3
			say "It doesn't mean that this room isn't familiar to you, though."
			flush
			sleep 3
			say "The only light comes from a plain lightbulb that is hanging on a cable from the ceiling in the middle of the room, but it is more than enough to see everything. "
			say "The large room is pretty tidy, all the boxes with stuff, tools and machinery are neatly sorted into shelves on the left and right walls. The storage rack on the 
					right is especial heavily loaded."
			say "On the back wall there is the workbench your father used to work at when he was still there."
			wait
			say "\n\"Get the ball!\", you hear your former self calling from the left."
			wait for 2 seconds
			play "190796__jiserte__xut1_ball_kick.ogg" on audio channel "sound"
			wait for 2 seconds
			say "Then that damn ball comes rolling at a high speed towards the shelve and crashes into it, bending one of the metal feet backwards. The boy who has once been 
					you comes running after it, closely followed by a furry creature that somehow appears blurry to you."
			wait for 2 seconds
			play "190796__jiserte__xut1_ball_hit.ogg" on audio channel "sound"
			wait
			say "With a loud sound the screws that held the rack on the wall at the top come shooting out of their holes."
			say "Someone seems to have pushed the \"slowmo\" button on the remote, because the following things are happening at a fraction of their normal speed."
			wait for 2 seconds
			play "screwShot.ogg" on audio channel "sound"
			wait
			say "\nYou see your other you turn his head towards you and you shiver at the evil grin on his face. Then the shelve falls over, the top frame hits the light bulb and 
					smashes it to pieces. For some reason it does not get any darker and you still have to watch everything for a second time."
			wait for 3 seconds
			play "breakingGlass.ogg" on audio channel "sound"
			wait
			say "After some of the heavy tools fall out and hit the two unfortunate friends to the ground, the metal structure comes crashing down upon them and you hear a terrible 
					whining sound from the creature."
			play "99960__cgeffex__large-crash.ogg" on audio channel "sound"
			wait for 4 seconds
			play "155321__jace__dog-whining.ogg" on audio channel "sound"
			wait
			say "You were lucky on that day that the real heavy stuff was lying on the other half of the rack. The buzz saw wasn't turned on and there wasn't even 
					a saw blade in it. But as you lay there on your stomach and the heavy machine hit your left arm, it didn't really matter."
			wait
			say "You are unconsciously holding your scar when you see a part of it penetrating the skin of your shadow and you hear the bone break in your mind again."
			wait for 2 seconds
			play "110617__soundscalpel-com__horror-bone-crunch.ogg" on audio channel "sound"
			wait
			say "Whining and heavy panting with a rattling sound in it fills the room. Your own shadow doesn't make a sound, but slowly turns his head and gazes evilly through 
					the twisted shelves at you."
			play "116486__adamlhumphreys__dogwhinepapillon01.ogg" on audio channel "sound"
			wait
			say "\nTears start to fill your eyes and run down your cheeks. You know that this will go on for hours. There will be no light, only frightening sounds."
			say "You will try to reach out, but you cannot get close enough. You will try to get out from under there, but you are not strong enough."
			say "Lying there in the dark, crying and hurting all over, you will shout for help, but no one will come."
			wait
			say "Until finally the whining will stop. So will the panting and the breathing altogether."
			stop audio channel "sound"
			wait for 10 seconds
			say "\n\"Enough!\", you shout between sobs, \"It was an accident! I'm sorry!\"\n"
			wait for 7 seconds
			say "\"So very sorry...\""
			wait
			say "\nSuddenly your other body fades away and the shelves are pushed upwards."
			wait for 2 seconds
			say "A large paw with claws then takes hold of the rack and throws it backwards with ease where it crashes into the workbench."
			wait for 20 seconds
			// start music here
			globals.fightMusic = "Motherlode.mp3"
			playMusic globals.fightMusic, 80
			say "\nYou are confronted with a tall furry creature, that is standing on its hind paws like a bear. Long, sharp claws are protuding from all of its paws and drool is 
					dripping from the teeth which are no less sharp, but somehow twisted."
			say "The head is smashed and one of its eyes is missing while blood is seeping out of the open cracks. The yellow fur is bloody, dirty and matted all over and a metal bar 
					is sticking in its chest, piercing the lungs."
			say "The long tongue licks over the pointy teeth before it goes into an attacking pose and starts the fight with a "
			scare 10, "threatening growl"
			say ".\n"
			wait for 20 seconds
		if !nightmare.killed
			startFight nightmare
