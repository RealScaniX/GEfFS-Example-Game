This is Cellar by ScaniX

there is a place called Cellar
	its ambience is "edjaGhosty.ogg"
	its exits are {"up": cellar door}
   its description is "This is without any doubt the cellar. There is - if that is possible - even more dust here than above. You can see a broken light bulb in the middle of
							the room, hanging from an old cable.\nThe dominating feature however is a large amount of boxes which is almost covering the whole floor. They are also stapled
							up to the ceiling or at least to a height over your head, so that you can barely see anything of the walls."
	its intro is @[cellarintro]

function cellarintro
	do
		say "The cellar. If the boys did their job right, the ball would be somewhere around here for sure."
		say "You really cannot help, but even a boy your age without a history like yours would get "
		fright 10, "frightened"
		say " down here."

there is a door called cellar door
	its origin is cellar
	its destination is entry hall
	its open is false
	its locked is true
   its key is rusty key

there is a key called rusty key
	its discovered is false
   its description is "You see a pretty rusty key."

when play starts or resumes
	now ev is a new event called "cellarEvent"
	ev.delay = 0
	ev.probability = 100
	ev.always = true
	ev.repeat = true
	ev.eventproc = "cellarEvent"
	add ev to cellar.events
	globals.cellarEvent = 0
	// stuff
	now crates is a new thing called "crates"
	crates.description = "They are closed and probably full of crap the old owner did not want anymore. The thick layer of dust let's you assume that the ball isn't hidden in there."
	crates.discovered = false
	add crates to cellar.items

function cellarEvent
	do
		if globals.cellarEvent is 0
			wait for 12 seconds
			say "Where the hell do those "
			fright 20, "freaky sounds"
			say " come from? "
			flush
			sleep 3
			say "Some old chimney? "
			flush
			sleep 1
			say "Water pipes? "
			flush
			sleep 1
			say "."
			flush
			sleep 0.3
			say "."
			flush
			sleep 0.3
			say ". "
			flush
			sleep 0.3
			say "Monsters?"
			this.delay = 1
			if player.light is not null
				now player.lightsteps is 2
		else if globals.cellarEvent is 1
			play "doorClose.ogg" on audio channel "sound" with volume 100
			now cellar door.open is false
			now cellar door.locked is true
			removeFromInv rusty key
			removeFromInv box of matches
			flush
			sleep 1
			say "What!? No!!! "
			fright 35, "Someone closed the cellar door.\n"
			wait for 3 seconds
			say "\"This isn't funny anymore.\", you shout out into the dark, \"Who is up there?\"\n"
			wait for 6 seconds
			say "When there is no answer, you run up the stairs to open the door. "
			wait for 3 seconds
			play "doorLocked.mp3" on audio channel "sound"
			flush
			sleep 0.8
			say "No. "
			play "doorLocked.mp3" on audio channel "sound"
			flush
			sleep 0.7
			say "No. "
			play "doorLocked.mp3" on audio channel "sound"
			flush
			sleep 0.6
			say "No! "
			wait for 2 seconds
			say "Why is it locked now?"
			wait for 2 seconds
			say "\"Hello?\", you shout again, \"Come on, let me out of here!\"\n"
			this.delay = 0
		else if globals.cellarEvent is 2
			say "You fumble around in your pockets, but the key is gone."
			this.delay = 2
			if player.light is not null
				now player.lightsteps is 4
		else
			addToInv box of matches
			globals.chapter = 1
			play "cat.ogg" on audio channel "sound"
			flush
			sleep 0.7
			say "\"Whoah!\", you jumped at the "
			fright 4, "sudden sound"
			say ". The fact that it was only a cat - wherever it suddenly appeared from - doesn't help you anymore as you are already too frightened."
			wait for 15 seconds
			say "The positive things are: It is already pitch black here, so there is no difference when the world goes black before your eyes. Another nice thing is
					that the crate you just collapsed upon seems to be stuffed with soft things."
			wait
			playmusic "Quinns Song-A New Man.mp3"
			playambience null
			cls
			saychapter 1
			fright 10
		globals.cellarEvent = globals.cellarEvent + 1
