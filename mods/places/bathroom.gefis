this is bathroom by scanix

a bathroom is a place
	its description is @[bathroomDesc]
	its ambience is @[bathroomAmbience]
	its ambienceVolume is 70
	attributes
		bool runningWater
	
there is a bathroom called lowerBathroom
	its name is "bathroom"
	its items are {mirror, sink, lowerBathroomDoor}
	its exits are {"east": lowerBathroomDoor}
	its intro is @[lowerBathroomIntro]
	its afterProc is "lowerBathroomAfterProc"
	its illumination is 10
	its regions are {"anywhere", "house", "first floor"}

there is a bathroom called upperBathroom
	its name is "bathroom"
	its items are {mirror, sink, bath tub, shower}
	its exits are {"north": westwingSouthDoor}
	its intro is @[upperBathroomIntro]
	its regions are {"anywhere", "house", "second floor"}

function bathroomAmbience
	do
		if this.runningWater
			return "runningWater.ogg"
		else
			return null

function lowerBathroomIntro
	do
		lowerBathroomDoor.name = "bathroom door"
		say "Entering the small room, you are delighted to notice that there are very few places for a monster to hide in here."

function upperBathroomIntro
	do
		westwingSouthDoor.name = "bathroom door"
		say "You step into the room and the first thing you notice is the echo of your steps caused by the tiled floor and walls."

there is a thing called mirror
	its description is @[bathroomMirrorDesc]
	its sound is "Looking at your reflection, you think that you might be better off listening to someone else."

there is a thing called bath tub

there is a thing called sink
	its useProc is "useSink"
	its sound is @[sinkSound]
	its taste is @[sinkTaste]
	its description is @[sinkDesc]

function lowerBathroomAfterProc
	do
		if lowerBathroomDoor.open
			lowerBathroom.illumination = 10
		else
			lowerBathroom.illumination = 0
		lightsource_update

function sinkDesc
	do
		append "Now covered in multiple layers of dust and grime, you guess that this has once been a nice old fashioned bathroom sink."
		if !globals.antidoteNoticed
			append " A small green flask is standing there. Seemingly less dusty than the rest."
			globals.antidoteNoticed = true
			add antidote to player.location.items

understand
	"use faucet" as "use sink"

action open faucet
	do
		if player.location.runningWater
			say "The water is already running."
		else
			do "use sink"

action close faucet
	do
		if !player.location.runningWater
			say "The faucet is already closed."
		else
			do "use sink"

function sinkSound
	do
		if player.location.runningWater
			return "The running water in the sink has a very specific effect on you. Good thing, that you are already in the bathroom..."
		else
			return "Straining your ears - if that is even possible - you finally end up with the astounding knowledge, that a closed faucet makes pretty much the same sound
					as a turned off radio. Who would have thought?"

function sinkTaste
	do
		if player.location.runningWater
			return "You drink a little of the water. It tastes pretty good... for tasteless water."
		else
			return "Tasting the closed faucet wouldn't quench your thirst. You briefly think about what fun it would be to lick across the sink and feel all that dust on
					your tongue... but decide against it."

function useSink
	do
		if player.location.runningWater
			say "You turn off the water again. Whoever has to pay for this will surely be thankful."
		else
			if !globals.sinkUsed
				globals.sinkUsed = true
				say "You are wondering whether the water still works in this abandoned house.\nIt obviously does."
			else
				say "You turn on the water."
		player.location.runningWater = !player.location.runningWater
		if player.location.runningWater
			loop "runningWater.ogg" on audio channel "ambience" with volume 70
		else
			stop audio channel "ambience"

there is a thing called shower

function bathroomDesc
	do
		append "It is a normal bathroom. There is a toilet, a sink and a mirror cabinet above it. "
		if this is upperBathroom
			append "This is quite a bit larger than your bathroom at home. It contains a shower <b>and</b> a bath tub."
		else
			append "[if lowerBathroomDoor.open]The blue light from the entry hall shines through the doorway into the small room and illuminates it. [end]This one is a bit smaller than the one at home, but you guess that it's because this one is only for guests."

function bathroomMirrorDesc
	do
		if globals.mirrorSmash is player.location.id
			if player.glow < 80
				say "You wonder if all the little yous in the mirror shards belong to the same person or if one of them is a better friend."
			else
				say "You wonder if all the little yous in the mirror shards are feeling better now like you do."
		else 
			say "The mirror is a bit dusty and covered in some grime, but you can still see 
					[if player.glow < 25]the face of a sad boy staring back at you[else if player.glow < 50]the face of a boy looking expectantly back at you[else if player.glow < 75]a
					boy that looks like you a lot[else]yourself[end]."
			if !globals.reflectionEncounterAdded
				globals.reflectionEncounterAdded = true
				addEncounter "house", reflection, 30
			else if player.glow < 35 and globals.mirrorSmash is null && chance of 70
				addOneTimeEvent "bathroomMirrorSmash"
			else if player.glow < 60 and chance of 30
				say "For a moment you think that the movements of the guy in the mirror do not match your own."

function bathroomMirrorSmash
	do
		if globals.mirrorSmash is not null
			return
		globals.mirrorSmash = player.location.id
		play "233607__agaxly__breaking-glass-1.ogg" on audio channel "sound" with volume 100
		flush
		sleep 2
		say "The sudden sound would have startled you. "
		flush
		sleep 2
		say "If it hasn't been your own fist that has smashed the mirror."
