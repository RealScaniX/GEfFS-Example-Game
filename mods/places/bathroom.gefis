this is bathroom by scanix

a bathroom is a place
	its description is @[bathroomDesc]
	its ambience is @[bathroomAmbience]
	its ambienceVolume is 70
	attributes
		bool runningWater
	
there is a bathroom called lowerBathroom
	its name is "bathroom"
	its items are {mirror, sink, toilet, lowerBathroomDoor}
	its exits are {"east": lowerBathroomDoor}
	its intro is @[lowerBathroomIntro]
	its afterProc is "lowerBathroomAfterProc"
	its illumination is 20
	its regions are {"anywhere", "house", "first floor"}

there is a bathroom called upperBathroom
	its name is "bathroom"
	its items are {mirror, sink, bath tub, shower, toilet, westwingSouthDoor}
	its exits are {"north": westwingSouthDoor}
	its illumination is 10
	its intro is @[upperBathroomIntro]
	its regions are {"anywhere", "house", "second floor"}

function bathroomAmbience
	do
		if this.runningWater
			return "runningWater.ogg"
		else
			return null

function lowerBathroomIntro
	do
		lowerBathroomDoor.name = "bathroom door"
		say "Entering the small room, you are delighted to notice that there are very few places for a monster to hide in here."

function upperBathroomIntro
	do
		westwingSouthDoor.name = "bathroom door"
		say "You step into the room and the first thing you notice is the echo of your steps caused by the tiled floor and walls."

there is a thing called mirror
	its description is @[bathroomMirrorDesc]
	its sound is "Looking at your reflection, you think that you might be better off listening to someone else."

there is a thing called sink
	its useProc is "useSink"
	its sound is @[sinkSound]
	its taste is @[sinkTaste]
	its description is @[sinkDesc]

there is a thing called toilet
	its useProc is "useToilet"
	its description is "Yes, this is a toilet."
	its smell is "It smells... not bad, for a toilet."
	its taste is "Seriously?"

function useToilet
	do
		say "You don't [one of]feel the urge[or]have to[at random]."

function lowerBathroomAfterProc
	do
		if lowerBathroomDoor.open
			lowerBathroom.illumination = 20
		else
			lowerBathroom.illumination = 10
		lightsource_update

function sinkDesc
	do
		append "Now covered in multiple layers of dust and grime, you guess that this has once been a nice old fashioned bathroom sink."
		if !globals.antidoteNoticed
			append " A small green flask is standing there. Seemingly less dusty than the rest."
			globals.antidoteNoticed = true
			add antidote to player.location.items

understand
	"use faucet" as "use sink"

action open faucet
	do
		if player.location.runningWater
			say "The water is already running."
		else
			do "use sink"

action close faucet
	do
		if !player.location.runningWater
			say "The faucet is already closed."
		else
			do "use sink"

function sinkSound
	do
		if player.location.runningWater
			return "The running water in the sink has a very specific effect on you. Good thing, that you are already in the bathroom..."
		else
			return "Straining your ears - if that is even possible - you finally end up with the astounding knowledge, that a closed faucet makes pretty much the same sound
					as a turned off radio. Who would have thought?"

function sinkTaste
	do
		if player.location.runningWater
			return "You drink a little of the water. It tastes pretty good... for tasteless water."
		else
			return "Tasting the closed faucet wouldn't quench your thirst. You briefly think about what fun it would be to lick across the sink and feel all that dust on
					your tongue... but decide against it."

function useSink
	do
		if player.location.runningWater
			say "You turn off the water again. Whoever has to pay for this will surely be thankful."
		else
			if !globals.sinkUsed
				globals.sinkUsed = true
				say "You are wondering whether the water still works in this abandoned house.\nIt obviously does."
			else
				say "You turn on the water."
		player.location.runningWater = !player.location.runningWater
		if player.location.runningWater
			loop "runningWater.ogg" on audio channel "ambience" with volume 70
		else
			stop audio channel "ambience"

function bathroomDesc
	do
		append "It is a normal bathroom. There is a toilet, a sink and a mirror cabinet above it. "
		if this is upperBathroom
			append "This is quite a bit larger than your bathroom at home. It contains a shower <b>and</b> a bath tub."
		else
			append "Some sparse moonlight makes it through the window[if lowerBathroomDoor.open] and blue light from the entry hall shines in through the doorway[end], illuminating 
						the small room enough to see. [end]This one is a bit smaller than the one at home, but you guess that it's because 
						this one is only for guests."

function bathroomMirrorDesc
	do
		if globals.mirrorSmash is player.location.id
			if player.glow < 80
				say "You wonder if all the little yous in the mirror shards belong to the same person or if one of them is a better friend."
			else
				say "You wonder if all the little yous in the mirror shards are feeling better now like you do."
		else 
			say "The mirror is a bit dusty and covered in some grime, but you can still see 
					[if player.glow < 25]the face of a sad boy staring back at you[else if player.glow < 50]the face of a boy looking expectantly back at you[else if player.glow < 75]a
					boy that looks like you a lot[else]yourself[end]."
			say "You think, that you look adventurous with your dark, messy hair which are almost long enough to cover your eyes, but your mother nevertheless always 
					says that you look cute."
			if nightmare.killed
				say "Your brown eyes now have a glint of hope in them."
			else
				say "Your brown eyes look a bit too serious, even for your own taste."
			if !globals.reflectionEncounterAdded
				globals.reflectionEncounterAdded = true
				addEncounter "house", reflection, 20
			else if player.glow < 60 and globals.mirrorSmash is null && chance of 70
				addOneTimeEvent "bathroomMirrorSmash"
			else if !reflection.killed and chance of 30
				say "For a moment you think that the movements of the guy in the mirror do not match your own."

function bathroomMirrorSmash
	do
		if globals.mirrorSmash is not null
			return
		globals.mirrorSmash = player.location.id
		play "233607__agaxly__breaking-glass-1.ogg" on audio channel "sound" with volume 100
		flush
		sleep 2
		say "The sudden sound would have startled you. "
		flush
		sleep 2
		say "If it hasn't been your own fist that has smashed the mirror."

there is a thing called shower
	its description is @[addOneTimeEvent "showerCurtain"]
	its smell is "Your nose tells you that this shower is in need of a bath."
	its taste is "In theory this will taste of dirt, limescale and yuck. You will leave the practical test to someone else."
	its useProc is "useShowerTub"

there is a thing called bath tub
	its description is "You half expect to find a skeleton lying inside of it or at least a pool of blood.\n
				But all you can see are some dead bugs on the bottom and a border of limescale and dirt."
	its smell is "Your nose tells you that this bath tub could use a shower."
	its taste is "In theory this will taste of dirt, limescale and yuck. You will leave the practical test to someone else."
	its useProc is "useShowerTub"

function useShowerTub
	do
		say "You don't want to get dirty."

function showerCurtain
	do
		if !globals.showerCurtainOpen
			say "The shower curtain is closed."
			wait for text
			say "You contemplate opening the shower curtain, but you are unsure if you are more afraid of what you might find or of not knowing what is in there."
			wait for text
			if chance of 50
				globals.showerCurtainOpen = true
				say "Finally you decide to open it."
				wait for text
				play "202014__ryancacophony__shower-curtain-fast.ogg" on audio channel "sound" with volume 100
				wait for 2 seconds
			else
				say "You decide against it."
		// no else, will continue from above!
		if globals.showerCurtainOpen
			say "Well, this is a pretty standard shower. The most evil thing about it is probably that it's so dirty, that you will need another shower after using it."
